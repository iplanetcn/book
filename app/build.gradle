apply plugin: 'com.android.application'


def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitTag = 'git describe --tags'.execute([], project.rootDir).text.trim()
def gitCommitCount = 'git rev-list --count HEAD'.execute([], project.rootDir).text.trim()
def gitUser = "git config user.name".execute().text.trim()

task printInfo {
    doLast {
        println("gitTag: " + gitTag)
        println("gitCommitCount: " + gitCommitCount)
        println("gitSha: " + gitSha)
        println("gitUser: " + gitUser)
    }
}

// Major version number (1.x.x)
def M = 1
// Minor version number (x.1.x)
def N = 2
// Patch version number (x.x.1)
def P = 3
// Revision number (e.g. x.x.x-rc1)
def R = ""
// Build number (revisions since last tag)
def B = gitCommitCount

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.phenix.app.book"
        minSdkVersion 29
        targetSdkVersion 29
        versionCode 100_0000 * M + 1000 * N +  P
        versionName M + "." + N + "." + P + (R.isEmpty() ? "" : ("-" + R))

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        //指定room.schemaLocation生成的文件路径
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.2.0'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'io.reactivex.rxjava3:rxjava:3.0.5'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation "androidx.room:room-runtime:2.2.5"
    implementation "androidx.room:room-rxjava2:2.2.5"
    implementation "androidx.room:room-guava:2.2.5"
    annotationProcessor "androidx.room:room-compiler:2.2.5"
    androidTestImplementation "androidx.room:room-testing:2.2.5"
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
